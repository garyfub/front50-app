buildscript {
	ext {
		springBootVersion = '1.2.8.RELEASE'
		springVersion = '4.1.9.RELEASE'
		front50Version = '1.42.0'
		front50AppVersion = '0.0.1.BUILD-SNAPSHOT'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

jar {
	baseName = 'front50-app'
	version = front50AppVersion
}

/**
 * No need to create your own Main. Simply use the one provided upstream.
 */
springBoot {
	mainClass = 'com.netflix.spinnaker.front50.Main'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven { url "http://spinnaker.bintray.com/gradle" }
}


dependencies {
	compile "com.netflix.spinnaker.front50:front50-web:${front50Version}"
	compile('org.codehaus.groovy:groovy')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

/**
 * Let's pin everything to the version of Spring Boot selected at the top
 */
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == 'org.springframework.boot') {
			details.useVersion springBootVersion
		}
		if (details.requested.group == 'org.springframework') {
			details.useVersion springVersion
		}
	}

	exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
	exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}
